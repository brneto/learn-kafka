import static org.gradle.api.file.DuplicatesStrategy.EXCLUDE

plugins {
    id 'java'
    id 'application'
    id 'io.freefair.lombok'
    id 'com.gorylenko.gradle-git-properties'
    id 'com.adarshr.test-logger'
}

group 'com.zinkworks.connectors'
version '0.0.1-SNAPSHOT'
description 'Kafka connector demo application'

java {
    sourceCompatibility '11'
}

repositories {
    mavenCentral()
}

dependencies {
    // Kafka
    implementation 'org.apache.kafka:connect-api:3.1.0'

    // Logging
    implementation 'ch.qos.logback:logback-classic:1.2.11'

    // Test
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

testlogger {
    theme 'mocha'
    showExceptions true
    showStackTraces true
    showFullStackTraces false
    showCauses true
    slowThreshold 2000
    showSummary true
    showSimpleNames false
    showPassed true
    showSkipped true
    showFailed true
    showStandardStreams false
    showPassedStandardStreams true
    showSkippedStandardStreams true
    showFailedStandardStreams true
    logLevel 'lifecycle'
}

test {
    useJUnitPlatform()
}

gitProperties {
    extProperty = 'gitProps'
}
generateGitProperties.outputs.upToDateWhen { false }

mainClassName = 'com.zinkworks.connectors.demo.ConnectorRunner'

jar {
    archiveBaseName = 'connect-file-event-source'
    duplicatesStrategy EXCLUDE
    from {
        configurations.runtimeClasspath
                .findAll { it.name.endsWith('jar') && !it.name.startsWith('slf4j-api') }
                .collect { zipTree it }
    }
    manifest {
        attributes(
            'Main-Class': "$mainClassName",
            'Built-By': System.properties['user.name'],
            'Build-Timestamp': Instant.now(),
            'Build-Revision': "${-> gitProps['git.commit.id']}",
            'Created-By': "Gradle ${gradle.gradleVersion}",
            'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
            'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

task deploy {
    dependsOn build
    doLast {
        String targetPath = 'kafka1:/opt/bitnami/kafka'
        exec {
            String resourcePath = sourceSets.main.output.resourcesDir
            String resourceName = 'connect-file-event-source.properties'

            println "docker cp $resourcePath/$resourceName $targetPath/config"
            executable 'docker'
            args 'cp', "$resourcePath/$resourceName", "$targetPath/config"
        }
        exec {
            Directory archivePath = jar.destinationDirectory.get()
            String archiveName = jar.archiveFileName.get()

            println "docker cp $archivePath/$archiveName $targetPath/libs"
            executable 'docker'
            args 'cp', "$archivePath/$archiveName", "$targetPath/libs"
        }
    }
}

// connect-standalone.sh $KAFKA_HOME/config/connect-standalone.properties $KAFKA_HOME/config/connect-file-event-source.properties
// kafka-console-consumer.sh --bootstrap-server localhost:9092 --topic dir_event
// kafka-topics.sh --bootstrap-server localhost:9092 --list
// kafka-topics.sh --bootstrap-server localhost:9092 --topic dir_event --create --partitions 3
// mkdir -p /opt/bitnami/test/toWatch
